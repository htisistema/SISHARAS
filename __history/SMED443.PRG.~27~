* CONFIRMACAO DE COMUNICADO
***************************
FUNCTION smed443
*****************
LOCAL mprg:='SMED443',mdocumento,opcao := ' ',;
      m_dupr:={},cons_semen:={},point:=0,cons_prop:={},cons_anim:={},m_doc := {},;
      mobs_pren := SPACE(60)

PRIVATE mcod_cli,mcod_ani

IF ! ver_nivel(mprg,'CONFIRMACAO DE COMUNICADO','12',nivel_acess)
        RETURN NIL
ENDIF
op_tela(0,0,40,120,'CONFIRMACAO DE COMUNICADO',,1)
WHILE .T.
        exibi_prg(mprg)
        mdata := mdata_sis
        mdocumento := SPACE(10)
        mcod_cli := mcod_ani := SPACE(5)
        limpa(00,00,40,120)
        exibi_prg(mprg)
        setcor(1)
        Mensagem('Preencha os Campos - <ESC> p/Abandonar')
        DEVPOS(0, 1);DEVOUT('No. Documento......:')
        DEVPOS(1, 1);DEVOUT('Codigo Cliente.....:')
        DEVPOS(2, 1);DEVOUT('Codigo da Animal...:')
        @ 3,0 TO 3,MAXCOL()
        DEVPOS(4, 1);DEVOUT('     Cliente                                        Animal                             Data Emissao   Documento Repass')
        @ 5,0 TO 5,MAXCOL()
        @ 0,22 GET mdocumento
        @ 1,22 GET mcod_cli PICT '99999' VALID IF(EMPTY(mcod_cli),.F.,ver_prop(mcod_cli,1,28)) WHEN EMPTY(mdocumento)
        READ
        IF LASTKEY() = 27
                EXIT
        ENDIF
        ver_prop(mcod_cli,1,28)
        @ 2,22 GET mcod_ani PICT '99999' VALID IF(EMPTY(mcod_ani),.F.,ver_animal(mcod_ani,2,28)) WHEN EMPTY(mcod_cli) .AND. EMPTY(mdocumento)
        READ
        IF LASTKEY() = 27
                LOOP
        ENDIF
        ver_animal(mcod_ani,2,28)
        opcao := op_simnao('S','Confirma os Dados:')
        IF opcao = "N"
                LOOP
        ENDIF
        cons_semen := {}
        IF ! EMPTY(mdocumento)
                sr_getconnection():exec("SELECT * FROM smedsemen WHERE documento = "+sr_cdbvalue(mdocumento)+" AND data_lib IS NOT NULL AND data_pren IS NULL",,.t.,@cons_semen)
        ELSEIF ! EMPTY(mcod_cli)
                sr_getconnection():exec("SELECT * FROM smedsemen WHERE cod_prop = "+sr_cdbvalue(mcod_cli)+" AND data_lib IS NOT NULL AND data_pren IS NULL",,.t.,@cons_semen)
        ELSEIF ! EMPTY(mcod_ani)
                sr_getconnection():exec("SELECT * FROM smedsemen WHERE cod_anim = "+sr_cdbvalue(mcod_ani)+" AND data_lib IS NOT NULL AND data_pren IS NULL",,.t.,@cons_semen)
        ELSE
                sr_getconnection():exec("SELECT * FROM smedsemen WHERE data_lib IS NOT NULL AND data_pren IS NULL",,.t.,@cons_semen)
        ENDIF
        IF LEN(cons_semen) = 0
                atencao('Nao foi encontrado nenhum registro')
                LOOP
        ENDIF
        i := 0
        m_dupr := {}
        m_doc := {}
        FOR i = 1 TO LEN(cons_semen)
                cons_prop:={}
                cons_anim:={}
                sr_getconnection():exec("SELECT razao FROM saccli WHERE cod_cli = "+sr_cdbvalue(cons_semen[i,3]),,.t.,@cons_prop)
                IF LEN(cons_prop) = 0
                        atencao('Nao foi encontrado este Cliente de Codigo: '+STR(cons_semen[i,3],5))
                        LOOP
                ENDIF
                sr_getconnection():exec("SELECT nome FROM smedcli WHERE registro = "+sr_cdbvalue(cons_semen[i,4]),,.t.,@cons_anim)
                IF LEN(cons_anim) = 0
                        atencao('Nao foi encontrado este Animal de Codigo: '+STR(cons_semen[i,4],5))
                        LOOP
                ENDIF

                                //  COD_PROP                    COD_ANIMAL                   DATA EMISSAO            DOCUMENTO
                AADD(m_dupr,' '+STR(cons_semen[i,3],5)+'-'+cons_prop[1,1]+' '+STR(cons_semen[i,4],5)+'-'+cons_anim[1,1]+'  '+DTOC(cons_semen[i,1])+'   '+cons_semen[i,2]+'   '+cons_semen[i,23])
                             // prop           doc
                AADD(m_doc,{cons_semen[i,3],cons_semen[i,2],cons_semen[i,5]})
        NEXT
        point := ACHOICE(6,1,39,119,m_dupr,,,point)
        DO CASE
                CASE LASTKEY()=27
                        LOOP
                CASE LASTKEY() = 13
                        op_tela(0,0,26,90,'COMUNICADO A ASSOCIACAO',,1)
                        setcor(1)
                        DEVPOS(0, 1);DEVOUT('Data do COMUNICADO.:')
                        DEVPOS(1, 1);DEVOUT('No.Registro da Egua:')
                        DEVPOS(2, 1);DEVOUT('Observacao.........:')
                        @ 3,0 TO 3,100
                        DEVPOS(4, 1);DEVOUT('                   R E P A S S E S')
                        DEVPOS(5, 1);DEVOUT('  Data    Documento   Nome'+SPACE(36)+'  CPF/CNPJ')
                        @ 6,0 TO 6,100
                        setcor(3)
                        DEVPOS(1,22);DEVOUT(m_doc[point,3])
                        cons_repass := {}
                        sr_getconnection():exec("SELECT * FROM smedrepass WHERE documento = "+sr_cdbvalue(m_doc[point,2]),,.t.,@cons_repass)
                        IF LEN(cons_repass) > 0
                                y:=0
                                FOR y = 1 TO LEN(cons_repass)
                                        DEVPOS(6+y,1);DEVOUT(DTOC(cons_repass[y,1])+'  '+cons_repass[y,2]+'  '+cons_repass[y,4]+'  '+cons_repass[y,5]+'  '+cons_repass[y,12])
                                        IF y > 20
                                                SCROLL(7,0,25,90,1)
                                        ENDIF
                                NEXT
                        ENDIF
                        @ 0,22 GET mdata
                        @ 2,22 GET mobs_pren
                        READ
                        IF LASTKEY() = 27
                                wvw_lclosewindow()
                                EXIT
                        ENDIF
                        opcao := op_simnao('S','Confirma a COMUNICADO:')
                        wvw_lclosewindow()
                        IF opcao = "N"
                                LOOP
                        ENDIF
                        sr_getconnection():exec("UPDATE smedsemen SET data_pren = "+sr_cdbvalue(mdata)+",oper_pren = "+sr_cdbvalue(cod_operado)+",obs_pren = "+sr_cdbvalue(mobs_pren)+" WHERE documento = "+sr_cdbvalue(m_doc[point,2]),,.f.)
                        sr_getconnection():exec('COMMIT',,.f.)
        ENDCASE
ENDDO
wvw_lclosewindow()
RETURN NIL
